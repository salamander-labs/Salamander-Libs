apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '0.0.3-beta01' // your library version
group 'com.salamander' // whatever group name you'd like

publishing {
    publications {
        maven(MavenPublication) {
            artifact("$buildDir/outputs/aar/logger-release.aar") // replace with your library name
            groupId this.group
            artifactId 'logger' // replace with your library artifact id
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    configurations = ['archives']
    // Default: false. Whether to run this as dry-run, without deploying
    dryRun = false
    // Default: false. Whether to override version artifacts already published
    override = true
    // Default: false. Whether version should be auto published after an upload
    publish = true
    pkg {
        userOrg = 'salamander-labs'
        repo = 'library' // the name of the repository you created on Bintray
        name = 'logger' // the name of the package you created inside it
        desc = 'Logging Module'
        websiteUrl = 'https://github.com/salamander-labs/Salamander-Libs'
        vcsUrl = 'https://github.com/salamander-labs/Salamander-Libs.git'
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}